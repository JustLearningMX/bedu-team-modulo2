plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id "org.asciidoctor.jvm.convert" version "3.3.2"

	//Plugin de sonarqube
	id "org.sonarqube" version "4.2.1.3168"

	//Cobertura de codigo
	id 'jacoco'

}

configurations {
	asciidoctorExt
}

group = 'com.bedu'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.4'
	implementation group: 'io.projectreactor', name: 'reactor-core', version: '3.5.5'
	runtimeOnly 'com.mysql:mysql-connector-j'
	testCompileOnly("org.assertj:assertj-core:3.23.1")
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compileOnly 'org.projectlombok:lombok:1.18.26'
	annotationProcessor 'org.projectlombok:lombok:1.18.26'

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	testCompileOnly 'org.projectlombok:lombok:1.18.26'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:0.0.1-SNAPSHOT'

}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test
}

sonar {
	properties {
		property "sonar.junit.reportPaths", "${buildDir}/test-results/junit-platform/"
		property "sonar.jacoco.reportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
	}
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

tasks.named('test') {
	useJUnitPlatform()

	// Configuraci√≥n para obtener el reporte en formato XML
	reports {
		junitXml.destination = file("${buildDir}/test-results/junit-platform/")
		junitXml.required = true
	}

	finalizedBy jacocoTestReport
}

tasks.withType(Test).configureEach {
	def outputDir = reports.junitXml.outputLocation
	jvmArgumentProviders << ({
		[
				"-Djunit.platform.reporting.open.xml.enabled=true",
				"-Djunit.platform.reporting.output.dir=${outputDir.get().asFile.absolutePath}"
		]
	} as CommandLineArgumentProvider)
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}